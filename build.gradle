buildscript {
    repositories {
        // Essential for the extractor dependency
        maven {
            url "https://releases.jfrog.io/artifactory/oss-releases/"
        }
        // Explicitly define the Gradle Plugin Portal URL
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        mavenCentral()
	// Option 1: Use the Gradle Plugin Portal (recommended for newer versions)
	// gradlePluginPortal()
	// Option 2: Use JCenter (legacy option)
	// jcenter() 
    }
    dependencies {
        // classpath group: 'org.jfrog.buildinfo', name: 'build-info-extractor-gradle', version: '4.4.0'
        classpath group: 'org.jfrog.buildinfo', name: 'build-info-extractor-gradle', version: '5.2.3'
    }
}

plugins {
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}
// Run this command to build: ./gradlew shadowJar

group 'org.openmbee.junit'
version '1.0.0'

apply plugin: 'java'
apply plugin: 'idea'

sourceCompatibility = 11

String buildTag = project.getProperties().get('buildTag')
if (buildTag != null && buildTag != version) {
    throw new GradleException('Version mismatch.')
}
if (buildTag == null || buildTag.isEmpty() || project.getProperties().getOrDefault('artifactoryRepository', '').toLowerCase().contains('snapshot')) {
    version += '-SNAPSHOT'
}
String buildNumber = project.getProperties().getOrDefault('buildNumber', System.currentTimeSeconds());

repositories {
    // Explicitly define the Gradle Plugin Portal URL
    maven {
	url "https://plugins.gradle.org/m2/"
    }
    mavenCentral()
    // Option 1: Use the Gradle Plugin Portal (recommended for newer versions)
    // gradlePluginPortal()
    // Option 2: Use JCenter (legacy option)
    // jcenter() 
}

dependencies {
    // Replaces 'compile' for Lombok
    compileOnly 'org.projectlombok:lombok:1.18.30'
    annotationProcessor 'org.projectlombok:lombok:1.18.30'
    // compile "org.projectlombok:lombok:1.16.16"

    implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.5'
    implementation group: 'commons-io', name: 'commons-io', version: '2.5'

    implementation group: 'javax.xml.bind', name: 'jaxb-api', version: '2.2.12'
    implementation group: 'com.sun.xml.bind', name: 'jaxb-core', version: '2.2.11'
    implementation group: 'com.sun.xml.bind', name: 'jaxb-impl', version: '2.2.11'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
}

apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.artifactory'

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar {
                archiveClassifier = 'sources'
            }
            artifact javadocJar {
                archiveClassifier = 'javadoc'
            }
        }
    }
}

artifactory {
    contextUrl = project.getProperties().get('artifactoryUrl')
    publish {
        repository {
            repoKey = project.getProperties().get('artifactoryRepository')
            username = project.getProperties().get('artifactoryUsername')
            password = project.getProperties().get('artifactoryPassword')
        }
        defaults {
            publications('mavenJava')
        }
        clientConfig.setIncludeEnvVars(true)
        if (buildNumber != null && !buildNumber.isEmpty()) {
            clientConfig.info.setBuildNumber(buildNumber)
        }
    }
}
artifactoryPublish.dependsOn(assemble, sourcesJar, javadocJar)
